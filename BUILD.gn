# Copyright 2017 The Fuchsia Authors
#
# Permission is hereby granted, free of charge, to any person obtaining a
# copy of this software and associated documentation files (the "Software"),
# to deal in the Software without restriction, including without limitation
# the rights to use, copy, modify, merge, publish, distribute, sublicense,
# and/or sell copies of the Software, and to permit persons to whom the
# Software is furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice (including the next
# paragraph) shall be included in all copies or substantial portions of the
# Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
# FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
# IN THE SOFTWARE.

config("vulkan_build_config") {
  include_dirs = [ "include" ]
  defines = [ "VK_USE_PLATFORM_MAGMA_KHR=1" ]
}

group("vulkan") {
  public_configs = [ ":vulkan_build_config" ]
  deps = [
    # the loader is libvulkan
    "loader",
  ]
}

group("extra_vulkan_headers") {
  public_configs = [ ":include_extra_vulkan_headers"]
  deps = [ ":generate_extra_vulkan_headers" ]
}

config("include_extra_vulkan_headers") {
  include_dirs = [ "$target_gen_dir/generated/include" ]
}

action("generate_extra_vulkan_headers") {
  script = "build-fuchsia/fuchsia-generate.sh"
  args = [ rebase_path(target_gen_dir, "." ) ]
  inputs = [
    "scripts/dispatch_table_helper_generator.py",
    "scripts/helper_file_generator.py",
    "scripts/loader_extension_generator.py",
    "scripts/lvl_genvk.py",
    "scripts/object_tracker_generator.py",
    "scripts/parameter_validation_generator.py",
    "scripts/threading_generator.py",
    "scripts/unique_objects_generator.py",
  ]
  sources = [
    "include/vulkan/vulkan.h",
    "scripts/vk.xml",
  ]
  outputs = [
    "$target_gen_dir/generated/include/vk_safe_struct.h",
    "$target_gen_dir/generated/include/vk_safe_struct.cpp",
    "$target_gen_dir/generated/include/vk_struct_size_helper.h",
    "$target_gen_dir/generated/include/vk_struct_size_helper.c",
    "$target_gen_dir/generated/include/vk_enum_string_helper.h",
    "$target_gen_dir/generated/include/vk_extension_helper.h",
    "$target_gen_dir/generated/include/vk_object_types.h",
    "$target_gen_dir/generated/include/vk_dispatch_table_helper.h",
    "$target_gen_dir/generated/include/thread_check.h",
    "$target_gen_dir/generated/include/parameter_validation.h",
    "$target_gen_dir/generated/include/unique_objects_wrappers.h",
    "$target_gen_dir/generated/include/vk_loader_extensions.h",
    "$target_gen_dir/generated/include/vk_loader_extensions.c",
    "$target_gen_dir/generated/include/vk_layer_dispatch_table.h",
  ]
}
